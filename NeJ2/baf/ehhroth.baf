IF
  Global("LKingDead","GLOBAL",1)
  Global("VP_LizardKingDead","LOCALS",0)
  InParty(Myself)
  !StateCheck(Player1,STATE_SLEEPING)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_LizardKingDead","LOCALS",1)
    StartDialogNoSet([PC])
END

IF
  Global("VP_ErevainJournal","GLOBAL",1)
  RealGlobalTimerExpired("VP_EreJournalDelay","GLOBAL")
  PartyHasItem("EREJOUR")  // ~Erevain‘s Journal~
  InParty(Myself)
  !StateCheck(Player1,STATE_SLEEPING)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_ErevainJournal","GLOBAL",2)
    StartDialogNoSet([PC])
END

IF
  Global("VP_ResqueLeina","GLOBAL",9)
  Global("VP_Hrothgar_FFBart","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("VP_Hrothgar_FFBart","GLOBAL",1)
    Dialog(Player1)
END

IF
  Global("VP_Hrothgar_Tilorn","GLOBAL",2)
  RealGlobalTimerExpired("VP_HrothgarTalks","GLOBAL")
  InParty(Myself)
  !StateCheck(Player1,STATE_SLEEPING)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_Hrothgar_Tilorn","GLOBAL",3)
    StartDialogNoSet([PC])
END

IF
  Global("VP_Hrothgar_Tilorn","GLOBAL",4)
  GlobalTimerExpired("VP_HrothgarReminds","GLOBAL")
  !AreaCheck("TT9000")
  InParty(Myself)
  !StateCheck(Player1,STATE_SLEEPING)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_Hrothgar_Tilorn","GLOBAL",5)
    StartDialogNoSet([PC])
END

IF
  Global("VP_Hrothgar_Tilorn","GLOBAL",6)
  GlobalTimerExpired("VP_HrothgarReminds","GLOBAL")
  InParty(Myself)
  !StateCheck(Player1,STATE_SLEEPING)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_Hrothgar_Tilorn","GLOBAL",7)
    StartDialogNoSet([PC])
END

IF
  Global("VP_SoW_Portal_Talk","GLOBAL",1)
  Global("VP_Who_Is_Hrothgar","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("VP_Who_Is_Hrothgar","GLOBAL",1)
    StartDialogNoSet([PC])
END

IF
  Global("MURDER_ALARM","GLOBAL",1)
THEN
  RESPONSE #100
    StartDialogNoSet([PC])
END

IF
  Allegiance(Myself,ENEMY)
  !InParty(Myself)
  Global("NPCFight","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("NPCFight","LOCALS",1)
    ChangeAIScript("hrothx",CLASS)
    ChangeAIScript("useitem",GENERAL)
    ChangeAIScript("",DEFAULT)
    SetDialogue("")
    Continue()
END

IF
  Allegiance(Myself,NEUTRAL)
  AttackedBy([GOODCUTOFF],DEFAULT)
THEN
  RESPONSE #100
    ChangeAIScript("hrothx",CLASS)
    ChangeAIScript("useitem",GENERAL)
    ChangeAIScript("",DEFAULT)
    Enemy()
    Continue()
END

IF
  !Allegiance(Myself,ENEMY)
  !InParty(Myself)
  Global("NPCFight","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("NPCFight","LOCALS",0)
    ChangeAIScript("",CLASS)
    ChangeAIScript("",GENERAL)
    Continue()
END

IF
  !Allegiance(Myself,ENEMY)
  OR(2)
    Global("EHTOWNIE_HOSTILE","MYAREA",1)
    Global("MURDER_ALARM","GLOBAL",1)
THEN
  RESPONSE #100
    Enemy()
    Continue()
END

///////////////////////////////////

//Banter 1 - Day 3
IF
  InParty(Myself)
  Global("VP_BPC","LOCALS",0)
  Global("VP_Start_Banters","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("VP_BPC","LOCALS",1)
    SetGlobalTimer("VP_Hrothgar_PC","GLOBAL",TWO_DAYS)
END

IF
  GlobalTimerExpired("VP_Hrothgar_PC","GLOBAL")
  InParty(Myself)
  See(Player1)
  !StateCheck(Player1,STATE_SLEEPING)
  CombatCounter(0)
  Global("VP_BPC","LOCALS",1)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_BPC","LOCALS",2)
    Interact(Player1)
END

//Banter 2 - Day 3
IF
  InParty(Myself)
  Global("VP_BPC","LOCALS",3)
  Global("VP_In_IWD_Part1","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("VP_BPC","LOCALS",4)
    RealSetGlobalTimer("VP_Hrothgar_PC","GLOBAL",180)
END

IF
  RealGlobalTimerExpired("VP_Hrothgar_PC","GLOBAL")
  InParty(Myself)
  See(Player1)
  !StateCheck(Player1,STATE_SLEEPING)
  CombatCounter(0)
  Global("VP_BPC","LOCALS",4)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_BPC","LOCALS",5)
    Interact(Player1)
END

//Podstraxovka/////////////////////

//IF
//  InParty(Myself)
//  Global("VP_BPC_Check","LOCALS",0)
//  Global("VP_BPC","LOCALS",5)
//  Global("VP_In_IWD_Part1","GLOBAL",1)
//THEN
//  RESPONSE #100
//    SetGlobal("VP_BPC","LOCALS",4)
//END

//Banter 3 with PC - after coming back from IWD
IF
  InParty(Myself)
  Global("VP_In_IWD_Part1","GLOBAL",0)
  Global("VP_BPC","LOCALS",6)
THEN
  RESPONSE #100
    SetGlobal("VP_BPC","LOCALS",7)
    SetGlobalTimer("VP_Hrothgar_PC","GLOBAL",THREE_DAYS)
END

IF
  GlobalTimerExpired("VP_Hrothgar_PC","GLOBAL")
  InParty(Myself)
  See(Player1)
  !StateCheck(Player1,STATE_SLEEPING)
  CombatCounter(0)
  Global("VP_BPC","LOCALS",7)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_BPC","LOCALS",8)
    Interact(Player1)
END

//Banter with Dar - Day 11
IF
	InParty(Myself)
	InParty("Dar")
	!Dead("Dar")
	Global("VP_BDar","LOCALS",0)
	Global("VP_In_IWD_Part1","GLOBAL",1)
	Global("VP_Start_Banters","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("VP_BDar","LOCALS",1)
		SetGlobalTimer("VP_Hrothgar_Dar","GLOBAL",ELEVEN_DAYS)
END

IF
	GlobalTimerExpired("VP_Hrothgar_Dar","GLOBAL")
	InParty(Myself)
	InParty("Dar")
	See("Dar")
	!Dead("Dar")
	!StateCheck("Dar",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BDar","LOCALS",1)
	!See([ENEMY])
	Global("VP_BanterTalkDelay","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BanterTalkDelay","GLOBAL",1)
		RealSetGlobalTimer("VP_BanterTalkTimer","GLOBAL",10)
END

IF
	Global("VP_BanterTalkDelay","GLOBAL",1)
	RealGlobalTimerExpired("VP_BanterTalkTimer","GLOBAL")
	GlobalTimerExpired("VP_Hrothgar_Dar","GLOBAL")
	InParty(Myself)
	InParty("Dar")
	See("Dar")
	!Dead("Dar")
	!StateCheck("Dar",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BDar","LOCALS",1)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("VP_BanterTalkDelay","GLOBAL",0)
		SetGlobal("VP_BDar","LOCALS",2)
		Interact("Dar")
END

//Podstraxovka/////////////////////

//IF
//  InParty(Myself)
//  InParty("Dar")  // Dar
//  !Dead("Dar")  // Dar
//  Global("VP_In_IWD_Part1","GLOBAL",1)
//  Global("VP_BDar_Check","LOCALS",0)
//  Global("VP_BDar","LOCALS",2)
//  Global("VP_Start_Banters","GLOBAL",1)
//THEN
//  RESPONSE #100
//    SetGlobal("VP_BDar","LOCALS",1)
//END

///////////////////////////////////

//Banter 2 with Dar - after coming back from IWD
IF
	InParty(Myself)
	InParty("Dar")
	!Dead("Dar")
	Global("VP_In_IWD_Part1","GLOBAL",0)
	Global("VP_BDar","LOCALS",3)
THEN
	RESPONSE #100
		SetGlobal("VP_BDar","LOCALS",4)
		SetGlobalTimer("VP_Hrothgar_Dar","GLOBAL",FIVE_DAYS)
END

IF
	GlobalTimerExpired("VP_Hrothgar_Dar","GLOBAL")
	Global("VP_In_IWD_Part1","GLOBAL",0)
	InParty(Myself)
	InParty("Dar")
	See("Dar")
	!Dead("Dar")
	!StateCheck("Dar",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BDar","LOCALS",4)
	!See([ENEMY])
	Global("VP_BanterTalkDelay","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_BanterTalkDelay","GLOBAL",1)
		RealSetGlobalTimer("VP_BanterTalkTimer","GLOBAL",10)
END

IF
	GlobalTimerExpired("VP_Hrothgar_Dar","GLOBAL")
	Global("VP_In_IWD_Part1","GLOBAL",0)
	InParty(Myself)
	InParty("Dar")
	See("Dar")
	!Dead("Dar")
	!StateCheck("Dar",STATE_SLEEPING)
	CombatCounter(0)
	Global("VP_BDar","LOCALS",4)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("VP_BanterTalkDelay","GLOBAL",0)
		SetGlobal("VP_BDar","LOCALS",5)
		Interact("Dar")
END

//Banter with Jaheira - Day 3
IF
  InParty(Myself)
  InParty("Jaheira")  // Jaheira
  !Dead("Jaheira")  // Jaheira
//	!Global("SPRITE_IS_DEADKHALID","GLOBAL",1)
  Global("VP_BJaheira","LOCALS",0)
  Global("VP_In_IWD_Part1","GLOBAL",1)
  Global("VP_Start_Banters","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("VP_BJaheira","LOCALS",1)
    SetGlobalTimer("VP_Hrothgar_Jaheira","GLOBAL",THREE_DAYS)
END

IF
  GlobalTimerExpired("VP_Hrothgar_Jaheira","GLOBAL")
  Global("VP_In_IWD_Part1","GLOBAL",1)
  InParty(Myself)
  InParty("Jaheira")  // Jaheira
  See("Jaheira")  // Jaheira
  !Dead("Jaheira")  // Jaheira
  !StateCheck("Jaheira",STATE_SLEEPING)  // Jaheira
  CombatCounter(0)
  Global("VP_BJaheira","LOCALS",1)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_BJaheira","LOCALS",2)
    Interact("Jaheira")
END

IF
  RealGlobalTimerExpired("VP_Hrothar_Timer","GLOBAL")
  Global("VP_Know_Caius","GLOBAL",3)
THEN
  RESPONSE #100
    SetGlobal("VP_Know_Caius","GLOBAL",4)
    StartDialogNoSet(Player1)
END

IF
  RealGlobalTimerExpired("VP_Hrothar_Timer","GLOBAL")
  Global("VP_Leaving_Halruaa","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("VP_Leaving_Halruaa","GLOBAL",2)
    StartDialogNoSet(Player1)
END

IF
  GlobalTimerExpired("VP_Leaving_Timer","LOCALS")
  Global("KickedOut","LOCALS",1)
  Global("VP_Break_NeJ_Plot","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("VP_Break_NeJ_Plot","GLOBAL",2)
    DestroySelf()
END

IF
  InParty(Myself)
  OR(4)
    !InParty("Imoen")  // ~Imoen~
    !InParty("Imoen2")  // Imoen2
    Dead("Imoen")  // ~Imoen~
    Dead("Imoen2")  // Imoen2
  See(Player1)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("SPRITE_IS_DEADYXUNOMEI","GLOBAL",1)
  Global("VP_YxunomeiDead","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("VP_YxunomeiDead","GLOBAL",1)
    StartDialogNoSet(Player1)
END

//27.07.2017////////////////////////////////////////////////////////

IF
  RealGlobalTimerExpired("VP_DragonsEye","GLOBAL")
  Global("VP_DragonsEyeNotes","GLOBAL",0)
  Global("VP_DE_NotesTimer","GLOBAL",1)
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_DragonsEyeNotes","GLOBAL",1)
    StartDialogNoSet(Player1)
END

/////////////////////////////////////////////////////////////////////////

IF
  PartyHasItem("PWARJ")
  InParty(Myself)
  Global("Presio_Journal","GLOBAL",0)
  Global("VP_DialogDelay","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("VP_Who_Is_OldEnemy","GLOBAL",10)
    SetGlobal("VP_DialogDelay","GLOBAL",1)
END

IF
  RealGlobalTimerExpired("VP_Who_Is_OldEnemy","GLOBAL")
  PartyHasItem("PWARJ")
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("Presio_Journal","GLOBAL",0)
  Global("VP_DialogDelay","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("Presio_Journal","GLOBAL",2)
    SetGlobal("VP_DialogDelay","GLOBAL",0)
    StartDialogNoSet(Player1)
END

IF
  PartyHasItem("TuLette4")  // ~Letter~
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_Leveller_Quest","GLOBAL",0)
  Global("VP_DialogDelay","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("VP_LetDialogDelay","GLOBAL",10)
    SetGlobal("VP_DialogDelay","GLOBAL",1)
END

IF
  RealGlobalTimerExpired("VP_LetDialogDelay","GLOBAL")
  PartyHasItem("TuLette4")  // ~Letter~
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_Leveller_Quest","GLOBAL",0)
  Global("VP_DialogDelay","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("VP_Leveller_Quest","GLOBAL",1)
    SetGlobal("VP_DialogDelay","GLOBAL",0)
    StartDialogNoSet(Player1)
END

IF
  PartyHasItem("TuLette5")  // ~Letter~
  InParty(Myself)
  OR(2)
    !InParty("Minsc")  // Minsc
    !See("Minsc")  // Minsc
  OR(2)
    !InParty("Dar")  // Dar
    !See("Dar")  // Dar
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_Leveller_Quest","GLOBAL",2)
  Global("VP_DialogDelay","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("VP_LetDialogDelay","GLOBAL",10)
    SetGlobal("VP_DialogDelay","GLOBAL",1)
END

IF
  RealGlobalTimerExpired("VP_LetDialogDelay","GLOBAL")
  PartyHasItem("TuLette5")  // ~Letter~
  InParty(Myself)
  OR(2)
    !InParty("Minsc")  // Minsc
    !See("Minsc")  // Minsc
  OR(2)
    !InParty("Dar")  // Dar
    !See("Dar")  // Dar
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_Leveller_Quest","GLOBAL",2)
  Global("VP_DialogDelay","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("VP_Leveller_Quest","GLOBAL",3)
    SetGlobal("VP_DialogDelay","GLOBAL",0)
    StartDialogNoSet(Player1)
END

IF
  PartyHasItem("TuLette6")  // ~Letter~
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_Leveller_Quest","GLOBAL",4)
  Global("VP_DialogDelay","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("VP_LetDialogDelay","GLOBAL",10)
    SetGlobal("VP_DialogDelay","GLOBAL",1)
END

IF
  RealGlobalTimerExpired("VP_LetDialogDelay","GLOBAL")
  PartyHasItem("TuLette6")  // ~Letter~
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_Leveller_Quest","GLOBAL",4)
  Global("VP_DialogDelay","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("VP_Leveller_Quest","GLOBAL",5)
    SetGlobal("VP_DialogDelay","GLOBAL",0)
    StartDialogNoSet(Player1)
END

IF
  PartyHasItem("TuLette7")  // ~Letter~
  PartyHasItem("TuLette8")  // ~Letter~
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_Leveller_Quest","GLOBAL",7)
  Global("VP_DialogDelay","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("VP_LetDialogDelay","GLOBAL",10)
    SetGlobal("VP_DialogDelay","GLOBAL",1)
END

IF
  RealGlobalTimerExpired("VP_LetDialogDelay","GLOBAL")
  PartyHasItem("TuLette7")  // ~Letter~
  PartyHasItem("TuLette8")  // ~Letter~
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_Leveller_Quest","GLOBAL",7)
  Global("VP_DialogDelay","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("VP_Leveller_Quest","GLOBAL",8)
    SetGlobal("VP_DialogDelay","GLOBAL",0)
    StartDialogNoSet(Player1)
END

IF
  PartyHasItem("Leveller")  // ~Leveller~
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_DialogDelay","GLOBAL",0)
  OR(2)
    Global("VP_Leveller_Quest","GLOBAL",9)
    Global("VP_Leveller_Quest","GLOBAL",10)
THEN
  RESPONSE #100
    RealSetGlobalTimer("VP_LetDialogDelay","GLOBAL",10)
    SetGlobal("VP_DialogDelay","GLOBAL",1)
END

IF
  RealGlobalTimerExpired("VP_LetDialogDelay","GLOBAL")
  PartyHasItem("Leveller")  // ~Leveller~
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
  Global("VP_DialogDelay","GLOBAL",1)
  OR(2)
    Global("VP_Leveller_Quest","GLOBAL",9)
    Global("VP_Leveller_Quest","GLOBAL",10)
THEN
  RESPONSE #100
    SetGlobal("VP_Leaving_Halruaa","GLOBAL",3)
    SetGlobal("VP_Leveller_Quest","GLOBAL",11)
    SetGlobal("VP_DialogDelay","GLOBAL",0)
    StartDialogNoSet(Player1)
END

IF
  Global("VP_Leaving_Halruaa","GLOBAL",7)
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_Leaving_Halruaa","GLOBAL",8)
    StartDialogNoSet(Player1)
END

IF
  PartyHasItem("TuLette5")  // ~Letter~
  Global("SPRITE_IS_DEADTuKimSia","GLOBAL",1)
  Global("VP_Know_KimSia","GLOBAL",2)
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_Know_KimSia","GLOBAL",3)
    StartDialogNoSet(Player1)
END

IF
  Global("VP_Necro_Test","GLOBAL",9)
  Global("SPRITE_IS_DEADKharesse","GLOBAL",1)
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_Necro_Test","GLOBAL",10)
    StartDialogNoSet(Player1)
END

IF
  Global("VP_Dar_Quest","GLOBAL",97)
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_Dar_Quest","GLOBAL",96)
    StartDialogNoSet(Player1)
END

IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	Global("VP_Enough_IsEnough","LOCALS",1)
THEN
	RESPONSE #100
		Enemy()
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	RandomNum(9,1)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,~I wouldn't try that again. Thieves are not welcome in these parts.~)
		SetGlobal("VP_Enough_IsEnough","LOCALS",1)
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	RandomNum(9,2)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,~That better not have been your hand I felt in my pocket.~)
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	RandomNum(9,3)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,~You thieving scoundrel. I'll let you off this time with a warning, but next time...~)
		SetGlobal("VP_Enough_IsEnough","LOCALS",1)
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	RandomNum(9,4)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,~Watch yourself, stranger. Pickpocketing is risky business - especially when your as clumsy as you seem to be.~)
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	RandomNum(9,5)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,~Hey, watch it! Keep your hands to yourself, friend.~)
		Continue()
END
		
IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	RandomNum(9,6)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,~I don't appreciate the attempt at theft. You'd better behave yourself.~)
		Continue()
END
		
IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	RandomNum(9,7)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,~You're lucky I'm a forgiving person. Don't try to steal from me again.~)
		SetGlobal("VP_Enough_IsEnough","LOCALS",1)
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	RandomNum(9,8)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,~Keep your distance. You think I can't tell what you were doing just now?~)
		Continue()
END
		
IF
	!Allegiance(Myself,ENEMY)
	PickPocketFailed([PC])
	RandomNum(9,9)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,~What were you just doing? I'd like to ask that you step away, thank you.~)
		Continue()
END

IF
  Global("VP_Hrothgar_Slayer","GLOBAL",1)
  InParty(Myself)
  CombatCounter(0)
  !StateCheck(Player1,STATE_SLEEPING)
  !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("VP_Hrothgar_Slayer","GLOBAL",2)
    StartDialogNoSet(Player1)
END