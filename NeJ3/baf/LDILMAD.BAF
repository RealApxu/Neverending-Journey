IF
  NumberOfTimesTalkedTo(0)
  See([PC])
  !Die()
THEN
  RESPONSE #100
    Dialogue([PC])
END

IF
	HPPercentLT(Myself,50)
	!RealGlobalTimerNotExpired("DrankPotionTimer","LOCALS")
	HasItem("POTN55",Myself) // Potion of Superior Healing
THEN
	RESPONSE #100
		RealSetGlobalTimer("DrankPotionTimer","LOCALS",6)
		DisplayStringHead(Myself,46150) // quaffs a potion
		UseItem("POTN55",Myself) // Potion of Superior Healing
END

IF
	HPPercentLT(Myself,60)
	Global("InvulnerabilityPotion","LOCALS",0)
	!RealGlobalTimerNotExpired("DrankPotionTimer","LOCALS")
	HasItem("POTN11",Myself) // Potion of Invulnerability
THEN
	RESPONSE #100
		RealSetGlobalTimer("DrankPotionTimer","LOCALS",6)
		SetGlobal("InvulnerabilityPotion","LOCALS",1)
		DisplayStringHead(Myself,46150) // quaffs a potion
		UseItem("POTN11",Myself) // Potion of Invulnerability
END

IF
	Global("DRANK_POTION","LOCALS",0)
	Allegiance(Myself,ENEMY)
	See([PC])
	HasItem("POTN07",Myself) // Potion of Storm Giant Strength
THEN
	RESPONSE #100
		SetGlobal("DRANK_POTION","LOCALS",1)
		DisplayStringHead(Myself,46150) // quaffs a potion
		UseItem("POTN07",Myself) // Potion of Storm Giant Strength
END

IF
	Global("DRANK_POTION","LOCALS",1)
	Allegiance(Myself,ENEMY)
	See([PC])
	HasItem("POTN14",Myself) // Oil of Speed
THEN
	RESPONSE #100
		SetGlobal("DRANK_POTION","LOCALS",2)
		DisplayStringHead(Myself,46150) // quaffs a potion
		UseItem("POTN14",Myself) // Oil of Speed
END

IF
	Global("MAP_GROUP_HOSTILE2","MYAREA",0)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("MAP_GROUP_HOSTILE2","MYAREA",1)
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	!Global("MAP_GROUP_HOSTILE2","MYAREA",0)
THEN
	RESPONSE #100
		Enemy()
		SetGlobal("MAP_GROUP_HOSTILE2","MYAREA",1)
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(Myself),75)
END

IF
	AttackedBy(NearestEnemyOf(Myself),DEFAULT)
THEN
	RESPONSE #100
		MoveToObject(LastAttackerOf(Myself))
END

IF
	!Allegiance(Myself,ENEMY)
	AttackedBy([GOODCUTOFF],DEFAULT)
	Global("MAP_GROUP_HOSTILE2","MYAREA",0)
THEN
	RESPONSE #100
		Enemy()
		SetGlobal("MAP_GROUP_HOSTILE2","MYAREA",1)
		Continue()
END

IF
	Allegiance(Myself,ENEMY)
	ActionListEmpty()
	!See([PC])
THEN
	RESPONSE #100
		MoveToObject([PC])
END


