IF
	!Global("SPRITE_IS_DEADEHOldJed","GLOBAL",0)
THEN
	RESPONSE #100
		DestroySelf()
END

IF
	Global("VP_Set_Jed_Timer","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("VP_Set_Jed_Timer","LOCALS",1)
		RealSetGlobalTimer("JED_FLOAT_TIMER","LOCALS",3)
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	AttackedBy([GOODCUTOFF],DEFAULT)
THEN
	RESPONSE #100
		Enemy()
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(Myself),75)
END

IF
	AttackedBy(NearestEnemyOf(Myself),DEFAULT)
THEN
	RESPONSE #100
		MoveToObject(LastAttackerOf(Myself))
END

IF
	Global("JED_FLOAT","LOCALS",0)
	RealGlobalTimerExpired("JED_FLOAT_TIMER","LOCALS")
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("JED_FLOAT","LOCALS",1)
		DisplayStringHead(Myself,~Arrrr cap'n ish a man who wears women's clothes... *burp*~)
		RealSetGlobalTimer("JED_FLOAT_TIMER","LOCALS",2)
END

IF
	Global("JED_FLOAT","LOCALS",1)
	RealGlobalTimerExpired("JED_FLOAT_TIMER","LOCALS")
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("JED_FLOAT","LOCALS",0)
		DisplayStringHead(Myself,~Avast there, maties! *hic* Come join Ol' Jed in a drink and few shee chanties.~)
		RealSetGlobalTimer("JED_FLOAT_TIMER","LOCALS",10)
END