IF
	NumTimesTalkedTo(0)
	See("Hrothgar")
	See(Player1)
	Global("VP_Know_Tilorn","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("VP_Know_Tilorn","GLOBAL",1)
		ActionOverride(Player1,StartDialog("Player1",Myself))
END

IF
	See(NearestEnemyOf(Myself))
	Global("Prep","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Prep","LOCALS",1)
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		ApplySpell(Myself,WIZARD_SPELL_TURNING)
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_ELECTRICITY)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Prep","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("Prep","LOCALS",2)
		ApplySpell(Myself,WIZARD_HASTE)
END

IF
	See(NearestEnemyOf(Myself))
	OR(7)
		StateCheck(LastSeenBy(Myself),STATE_HASTED)
		StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
		StateCheck(LastSeenBy(Myself),STATE_BLESS)
		StateCheck(LastSeenBy(Myself),STATE_CHANT)
		StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
		StateCheck(LastSeenBy(Myself),STATE_BLUR)
		StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
	Global("PrepDef","LOCALS",0)
THEN
	RESPONSE #70
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
		SetGlobal("PrepDef","LOCALS",1)
	RESPONSE #30
		ApplySpell(Myself,WIZARD_MIRROR_IMAGE)
		SetGlobal("PrepDef","LOCALS",1)
END

IF
	Allegiance(Myself,ENEMY)
	Detect([PC])
	!See(LastSeenBy(Myself))
	Global("PrepDef","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("PrepDef","LOCALS",2)
		ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)
END

IF
	See(NearestEnemyOf(Myself))
	CheckStatLT(Myself,1,STONESKINS)
	HitBy([ANYONE],CRUSHING)
	Global("Prep2","LOCALS",2)
THEN
	RESPONSE #100
		DisplayString(Myself,40199) // Chain Contingency - Mirror Image
		ApplySpell(Myself,WIZARD_MIRROR_IMAGE)
		DisplayString(Myself,3302) // Chain Contingency - Spell Turning
		ApplySpell(Myself,WIZARD_SPELL_TURNING)
		DisplayString(Myself,40240) // Chain Contingency - Mislead
		ApplySpell(Myself,WIZARD_MISLEAD)
		DisplayString(Myself,29736) // Contingency - Stoneskin
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		CreateVisualEffectObject("SPFLESHS",Myself)
		SetGlobal("Prep2","LOCALS",3)
END

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(LastSeenBy(Myself),STATE_STUNNED)
	Global("Disable","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Disable","LOCALS",1)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_NPC_SYMBOL_STUN)
END

IF
	See(NearestEnemyOf(Myself))
	Global("chain","LOCALS",0)
THEN
	RESPONSE #100
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_CHAIN_LIGHTNING)
		SetGlobal("chain","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Kill","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Kill","LOCALS",1)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
	See(NearestEnemyOf(Myself))
	Global("chain","LOCALS",1)
THEN
	RESPONSE #100
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_CHAIN_LIGHTNING)
		SetGlobal("chain","LOCALS",2)
END

IF
	See(NearestEnemyOf(Myself))
	HPLT(LastSeenBy(Myself),60)
	Global("Kill","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("Kill","LOCALS",2)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_POWER_WORD_KILL)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Kill","LOCALS",2)
THEN
	RESPONSE #100
		SetGlobal("Kill","LOCALS",3)
		DisplayString(Myself,39968) // Spell Trigger - Fired
		DisplayString(Myself,40244) // Spell Trigger - Stoneskins
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		DisplayString(Myself,40245) // Spell Trigger - Globe of Invulnerability
		ApplySpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
		DisplayString(Myself,40246) // Spell Trigger - Minor Spell Turning
		ApplySpell(Myself,WIZARD_MINOR_SPELL_TURNING)
		ApplySpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Disable","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("Disable","LOCALS",2)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_POWER_WORD_STUN)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Kill","LOCALS",3)
THEN
	RESPONSE #100
		SetGlobal("Kill","LOCALS",4)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_FINGER_OF_DEATH)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Kill","LOCALS",4)
THEN
	RESPONSE #100
		SetGlobal("Kill","LOCALS",5)
		DisplayString(Myself,39969) // Spell Sequencer - Fired
		DisplayString(Myself,40241) // Spell Sequencer - Stoneskins
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		DisplayString(Myself,40242) // Spell Sequencer - Improved Invisibility
		ApplySpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
		DisplayString(Myself,40243) // Spell Sequencer - Fireshield Red
		ApplySpell(Myself,WIZARD_FIRE_SHIELD_RED)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_FLESH_TO_STONE)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Kill","LOCALS",5)
THEN
	RESPONSE #100
		SetGlobal("Kill","LOCALS",6)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_DISINTEGRATE)
		CreateVisualEffectObject("SPFIREPI",LastSeenBy(Myself))
END

IF
	See(NearestEnemyOf(Myself))
	Global("Disable","LOCALS",2)
THEN
	RESPONSE #100
		SetGlobal("Disable","LOCALS",3)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_CHAOS)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Summon","LOCALS",0)
	AreaType(OUTDOOR)
THEN
	RESPONSE #100
		SetGlobal("Summon","LOCALS",1)
		ReallyForceSpell(Myself,WIZARD_WYVERN_CALL)
END

IF
	See(NearestEnemyOf(Myself))
	Global("Summon","LOCALS",1)
	AreaType(OUTDOOR)
THEN
	RESPONSE #100
		SetGlobal("Summon","LOCALS",2)
		ReallyForceSpell(Myself,WIZARD_WYVERN_CALL)
END

IF
	See(NearestEnemyOf(Myself))
	Global("PrepM","LOCALS",0)
THEN
	RESPONSE #100
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
		SetGlobal("PrepM","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	Global("PrepM","LOCALS",1)
THEN
	RESPONSE #100
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
		SetGlobal("PrepM","LOCALS",2)
END

IF
	See(NearestEnemyOf(Myself))
	Global("PrepM","LOCALS",2)
THEN
	RESPONSE #100
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_MELF_ACID_ARROW)
		SetGlobal("PrepM","LOCALS",3)
END

IF
	See(NearestEnemyOf(Myself))
	Global("PrepM","LOCALS",3)
THEN
	RESPONSE #100
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_MELF_ACID_ARROW)
		SetGlobal("PrepM","LOCALS",4)
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		Attack(NearestEnemyOf(Myself))
END

IF
	Global("VP_Leaving_Halruaa","GLOBAL",5)
THEN
	RESPONSE #100
		SetGlobal("VP_Leaving_Halruaa","GLOBAL",6)
		Dialogue("Hrothgar")
END
